load("@aspect_rules_swc//swc:defs.bzl", "swc")
load("@aspect_rules_ts//ts:defs.bzl", "ts_config", "ts_project")
load("@aspect_rules_js//js:defs.bzl", "js_binary", "js_library", "js_run_devserver")
load("@bazel_skylib//lib:partial.bzl", "partial")
load("@jest//:defs.bzl", "jest_test")
load("@npm//:@nestjs/cli/package_json.bzl", nest_bin = "bin")

SRC_PATTERNS = [
    "src/**/*.ts",
]

TEST_PATTERNS = [
    "src/**/*.test.ts",
    "src/**/*.spec.ts",
]

def nest_app(npm_deps):
    """
        Builds a nest app using swc.
    """

    ts_config(
        name = "tsconfig",
        src = "tsconfig.json",
        deps = ["//:tsconfig.node"],
    )

    SRCS = native.glob(include = SRC_PATTERNS, exclude = TEST_PATTERNS) + native.glob([".env*"])

    # This is a binary target that is generated by the @nestjs/cli package
    # Can be used to run the nest cli commands
    nest_bin.nest_binary(
        name = "nestcli",
    )

    # This runs the backend using nest cli
    # Needs to be benchmarked against the js_run_devserver
    # To see which is faster
    js_run_devserver(
        name = "dev",
        args = ["start", "--watch"],
        chdir = native.package_name(),
        tool = ":nestcli",
        data = SRCS + npm_deps + [
            ":tsconfig",
        ],
    )

    # For more information about using swc to transpile please see the ts_project_transpiler example
    # https://github.com/aspect-build/bazel-examples/tree/main/ts_project_transpiler
    ts_project(
        name = "build",
        srcs = SRCS,
        tsconfig = ":tsconfig",
        declaration = True,
        source_map = True,
        transpiler = partial.make(
            swc,
            swcrc = ".swcrc",
        ),
        deps = npm_deps,
    )

    js_library(
        name = "jest_config",
        srcs = ["jest.config.js"],
        visibility = ["//visibility:public"],
    )

    # This runs the backend using swc transpiler as the transpiler
    # Also references actual production build
    js_binary(
        name = "dev_swc",
        data = [
            ":build",

            # Added by swc during transpilation and used at runtime
            "//:node_modules/regenerator-runtime",
        ],
        entry_point = "src/main.js",
    )

    jest_test(
        name = "test",
        config = ":jest_config",
        data = [
            ":build",
        ] + npm_deps + native.glob(include = TEST_PATTERNS),
    )
